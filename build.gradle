buildscript {
  ext.versions = [
    'compileSdk': 27,
    'buildTools': '27.0.3',
    'minSdk': 14,
    'targetSdk': 27,
    'sourceCompatibility': JavaVersion.VERSION_1_7,
    'targetCompatibility': JavaVersion.VERSION_1_7,
    'okhttp': '3.10.0',
    'supportLibrary': '27.1.0'
  ]

  ext.deps = [
    androidPlugin: 'com.android.tools.build:gradle:3.0.1',
    okhttp: "com.squareup.okhttp3:okhttp:${versions.okhttp}",
    mockWebServer: "com.squareup.okhttp3:mockwebserver:${versions.okhttp}",
    pollexor: 'com.squareup:pollexor:2.0.4',
    supportCompat: "com.android.support:support-compat:${versions.supportLibrary}",
    supportFragment: "com.android.support:support-fragment:${versions.supportLibrary}",
    supportAnnotations: "com.android.support:support-annotations:${versions.supportLibrary}",
    supportExifInterface: "com.android.support:exifinterface:${versions.supportLibrary}",
    junit: 'junit:junit:4.12',
    truth: 'com.google.truth:truth:0.36',
    robolectric: 'org.robolectric:robolectric:3.1',
    mockito: 'org.mockito:mockito-core:1.9.5'
  ]

  ext.isCi = "true" == System.getenv('CI')

  repositories {
    jcenter()
    google()
  }

  dependencies {
    classpath deps.androidPlugin
  }
}

subprojects {
  repositories {
    jcenter()
    google()
  }

  tasks.withType(Test) {
    testLogging {
      events "failed"
      exceptionFormat "full"
      showExceptions true
      showStackTraces true
      showCauses true
    }
  }

  plugins.apply('checkstyle')

  task('checkstyle', type: Checkstyle) {
    configFile rootProject.file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'

    classpath = files()
  }

  group = GROUP
  version = VERSION_NAME

  ext {
    SNAPSHOT_REPOSITORY_URL = System.env.NEXUS_REPOSITORY
    SONATYPE_NEXUS_USERNAME = System.env.NEXUS_USER
    SONATYPE_NEXUS_PASSWORD = System.env.NEXUS_PASSWORD
  }

  afterEvaluate {
    tasks.findByName('check').dependsOn('checkstyle')
  }
}